*reset
#include "obaq.as"

//画面設定*************************************
physicsWidth=1000.0	// 物理演算画面サイズ
physicsHeight=375.0	// 
pictureWidth=1500.0	// 絵画画面サイズ
pictureHeight=600.0	// 
paletSize=300		// 喜怒哀楽画面サイズ
// 絵画画面
bgscr 0,pictureWidth,pictureHeight,,0,0
cls 4
title "picture"
line pictureWidth/2,pictureHeight/2,pictureWidth/2,pictureHeight/2
// 物理演算画面
bgscr 1,physicsWidth,physicsHeight,,0,pictureHeight
cls 4
title "drawing"
// 喜怒哀楽パレット
kidox=paletSize/2:kidoy=paletSize/2
bgscr 2,paletSize,paletSize,,pictureWidth,50
#uselib "gdi32" 
#cfunc CreateEllipticRgn "CreateEllipticRgn" int, int, int, int
#uselib "user32"
#func SetWindowRgn "SetWindowRgn" int, int, int
hRegion = CreateEllipticRgn (0, 0, paletSize, paletSize)
SetWindowRgn hwnd, hRegion, 1
// 筆調整画面
settingsize=500
bgscr 3,settingsize,settingsize,,pictureWidth,paletSize+50
brushper=100
circlesize=50
pu=1	// 1:[u]を前フレームで押してた,0:押してない
gosub *settingDraw
gsel 1
//*********************************************

// obaqの初期化
qreset

// オブジェクトの追加・設定********************
randomize
// 筆用オブジェクト
qaddpoly brush,50,physicsWidth/8,physicsHeight/8,,10,10,,,,2
qinertia brush,,0
qweight brush,100
qmat brush,mat_wire2,$000000,$FF0000
// モブ用オブジェクト
num=20
for i,0,2000,1
	if num==0:_break
	qaddpoly obj,rnd(5)+3,rnd(physicsWidth/4),rnd(physicsWidth/4),,rnd(5)+5,rnd(5)+5,,,,1
	if obj>=0:num--
	qweight obj,rnd(10)+1,rnd(10)+1
	qinertia obj,,0
	qmat obj,mat_wire2,$000000,$00FF00
next
// 外枠
qborder -physicsWidth/8.0,-physicsHeight/8.0,physicsWidth/8.0,physicsHeight/8
//*********************************************
*main
	gsel 2
	gosub *kidoairaku
	gsel 1
	// 筆に当たったオブジェクトの検出
	qcollision brush
	repeat
		qgetcol obj,colx,coly
		if obj<0:break
		// モブに当たった場所に円を描画
		gosub *circledraw
	loop
	// 筆の軌跡
	qgetpos brush,myx,myy,hoge
	qgetspeed brush,brushspeedx,brushspeedy,hoge
	gsel 0
	color r,g,b
	myx=myx*(pictureWidth/physicsWidth)*4
	myy=myy*(pictureHeight/physicsHeight)*4
	brushsize=sqrt(brushspeedx*brushspeedx+brushspeedy*brushspeedy)*brushper
	circle myx-brushsize,myy-brushsize,myx+brushsize,myy+brushsize,brushmode
	gsel 1
	// 筆の移動
	stick key,15
	if key&128 : end	// [ESC]キーで終了
	speedx=0.0:speedy=0.0	// カーソルキーで操作
	if key&1 : speedx-=0.005
	if key&4 : speedx+=0.005
	if key&2 : speedy-=0.005
	if key&8 : speedy+=0.005
	qspeed brush, speedx,speedy,0

	// 設定画面のキーボード操作
	getkey t,84
	getkey g,71
	getkey y,89
	getkey h,72
	getkey u,85
	getkey i,73
	
	if t:gosub *brushPlus
	if g:gosub *brushMinus
	if y:gosub *circlesizePlus
	if h:gosub *circlesizeMinus
	if u{
		if pu:gosub *brushmodeChange
		pu=0
	}else:pu=1
	if i:goto *reset
	gsel 1
	
	redraw 0
		// 背景初期化
		color:boxf
		// オブジェクトの更新
		qexec
		// オブジェクトの描画
		qdraw 1
	redraw 1
	
	await 1
	goto *main
	

*circledraw
	gsel 0
	color r,g,b
	colx=colx*(pictureWidth/physicsWidth)*4
	coly=coly*(pictureHeight/physicsHeight)*4
	circle colx-circlesize,coly-circlesize,colx+circlesize,coly+circlesize,0
	gsel 1
	return

*kidoairaku
	onclick gosub *mousepos
	getkey w,87
	getkey a,65
	getkey s,83
	getkey d,68
	if w:kidoy-=3
	if s:kidoy+=3
	if a:kidox-=3
	if d:kidox+=3
	// パレットの選択オブジェクトの壁判定
	if sqrt((kidox-paletSize/2)*(kidox-paletSize/2)+(kidoy-paletSize/2)*(kidoy-paletSize/2))>paletSize/2{
		kidoy=paletSize/2
		kidox=paletSize/2
	}
	//色相環描画******************************************
	dist=sqrt((kidox-paletSize/2)*(kidox-paletSize/2)+(kidoy-paletSize/2)*(kidoy-paletSize/2))	// 中心からの距離
	distPer=1.0-(dist\(paletSize/2))/(paletSize/2)	// 中心からの割合(0~1)
	radi=atan(kidoy-paletSize/2,kidox-paletSize/2)	//角度
	
	// 左上
	if kidox<=paletSize/2 && kidoy<=paletSize/2{
		r=-255.0*sin(radi)+(255.0+255.0*sin(radi))*distPer
		g=255.0
		b=255.0*distPer
	}
	// 右下
	if kidox>=paletSize/2 && kidoy>=paletSize/2{
		r=255.0*cos(radi)+(255.0-255.0*cos(radi))*distPer
		g=255.0*distPer
		b=255.0*sin(radi)+(255.0-255.0*sin(radi))*distPer
	}
	// 右上
	if kidox>paletSize/2 && kidoy<paletSize/2{
		r=255.0
		g=-255.0*sin(radi)+(255.0+255.0*sin(radi))*distPer
		b=255.0*distPer
	}
	// 左下
	if kidox<paletSize/2 && kidoy>paletSize/2{
		r=255.0*distPer
		g=-255.0*cos(radi)+(255.0+255.0*cos(radi))*distPer
		b=255.0*sin(radi)+(255.0-255.0*sin(radi))*distPer
	}
	
	redraw 0
		color r,g,b
		boxf
		color
		circle 0,0,paletSize,paletSize,0
		circle kidox-20,kidoy-20,kidox+20,kidoy+20
		line 0,paletSize/2,paletSize,paletSize/2
		line paletSize/2,0,paletSize/2,paletSize
		font "meiryo",30
		pos 0,paletSize/2
		mes "楽"
		pos paletSize/2-30,paletSize-40
		mes "哀"
		pos paletSize-40,paletSize/2
		mes "怒"
		pos paletSize/2-30,0
		mes "喜"
		pos paletSize/2,paletSize/2
		mes "無"
	redraw 1
	return

*mousepos
	gsel 2
	kidox=mousex
	kidoy=mousey
	return
	
*brushPlus
	brushper-=1
	gosub *settingDraw	
	return
*brushMinus
	brushper+=1
	gosub *settingDraw
	return

*circlesizePlus
	circlesize+=1
	gosub *settingDraw
	return
*circlesizeMinus
	circlesize-=1
	gosub *settingDraw
	return
*brushmodeChange
	brushmode++
	brushmode=brushmode\2
	gosub *settingDraw
	return
*settingDraw
	gsel 3
	redraw 0
		color 255,255,255:boxf
		color 0

		pos 0,0
		font "meiryo",30
		print "-(t)"
		print "筆サイズ"+brushper
		print "+(g)"
		
		pos 0,settingsize/2
		print "-(y)"
		print "円サイズ"+circlesize
		print "+(h)"
		circle 50,300,50+circlesize*2,300+circlesize*2,0		
		pos settingsize/2,0
		print "筆モード"
		circle settingsize/2,50,settingsize/2+50,100,brushmode
		
		pos settingsize/2,settingsize/2
		print "reset(i)"
		pos settingsize/2,settingsize/4*3
		button gosub "save",*savepicture
	redraw 1
	return
*savepicture
	dialog "bmp",17
	if stat==0:return
	gsel 0
	bmpsave refstr
	end